########################################################################
# Copyright (C) 2000,2001,2002,2006                                    #
# Shinobu Yoshimura, University of Tokyo,                              #
# the Japan Society for the Promotion of Science (JSPS)                #
# All Rights Reserved                                                  #
########################################################################

#============================================================
#
# configure.in
#
#============================================================

AC_INIT(Makefile.am)

# config_default='./config.default.'`hostname -s`
# echo looking for ${config_default}
# if test -f ${config_default}; then
#   echo evaluate ${config_default}
#   . ${config_default}
# fi

PACKAGE=AdvIO
MAJOR_VERSION=1.2
MINOR_VERSION=20060202-6
VERSION=${MAJOR_VERSION}
# VERSION=${MAJOR_VERSION}_${MINOR_VERSION}

AC_CANONICAL_HOST

AM_INIT_AUTOMAKE($PACKAGE, $VERSION)
AM_CONFIG_HEADER(advsys-config.h)

AC_SUBST(VERSION)
AC_DEFINE_UNQUOTED(MAJOR_VERSION, "$MAJOR_VERSION")
AC_DEFINE_UNQUOTED(MINOR_VERSION, "$MINOR_VERSION")

includedir='${prefix}'/include/Adv
AC_PREFIX_DEFAULT("$HOME"/ADVENTURE)

### DOC dir ################################3
AC_ARG_WITH(docdir, [  --with-docdir=DIR       Specify where to put the documentation [PREFIX/doc]],
        [adv_cv_docdir=$withval],
        [adv_cv_docdir='${prefix}'/doc])

docdir=$adv_cv_docdir
AC_SUBST(docdir)


#============================================================
# Checks for Tools
#============================================================

AC_PROG_CC
if test "${cross_compiling}" = "yes"; then
  echo CrossCompiling is not supported.
  exit 1;
fi
AC_PROG_LN_S
AC_PROG_RANLIB
AC_PROG_MAKE_SET
AC_SUBST(SET_MAKE)

# if test "${GCC}" = "yes"; then
#     ANSI_CFLAGS=-ansi
#     AC_SUBST(ANSI_CFLAGS)
# fi

AC_LANG_C
AC_REQUIRE_CPP

# case "${host}" in
# *freebsd*)
#   CC=egcc
#   CXX=/usr/local/bin/g++
#   GTK_CONFIG=/usr/X11R6/bin/gtk12-config
#   ;;
# esac

AC_ARG_ENABLE(cxx,
[  --disable-cxx       Do not inluclude support for c++] )

case ${enable_cxx} in
    no)   HAVE_CXX=no 
          CXX=":" ;;
    *)    HAVE_CXX=yes ;;
esac

if test "${HAVE_CXX}" = "yes" ; then
    AC_PROG_CXX
    AC_LANG_SAVE
    AC_LANG_CPLUSPLUS
    AC_REQUIRE_CPP
    AC_LANG_RESTORE
    AC_DEFINE(HAVE_CXX)
fi

AC_SUBST(HAVE_CXX)

#============================================================
# Checks for compiler features
#============================================================


#--------------------------------------------------
# namespace support
#--------------------------------------------------

if test "${HAVE_CXX}" = "yes" ; then

    AC_CACHE_CHECK(
      for namespace,
      adv_cv_cxx_namespace,
      [
        AC_LANG_SAVE
        AC_LANG_CPLUSPLUS
        AC_TRY_COMPILE(
          [namespace hoge {};],
          [],
          adv_cv_cxx_namespace=yes,
          adv_cv_cxx_namespace=no
        )
      ]
    )
    AC_LANG_RESTORE
    
    if test "${adv_cv_cxx_namespace}" = "yes"; then
      AC_DEFINE(CXX_HAVE_NAMESPACE)
    else
      AC_MSG_RESULT(cannot use namespace)
    fi


#--------------------------------------------------
# exception handling
#--------------------------------------------------

    AC_CACHE_CHECK(
      for C++ exception handling,
      adv_cv_cxx_exception,
      [
        AC_LANG_SAVE
        AC_LANG_CPLUSPLUS
        AC_TRY_COMPILE(
          [#include <stdexcept>],
          [try {} catch(...){}],
          adv_cv_cxx_exception=yes,
          adv_cv_cxx_exception=no
        )
      ]
    )
    AC_LANG_RESTORE
    
    if test "${adv_cv_cxx_exception}" = "yes"; then
      AC_DEFINE(CXX_HAVE_EXCEPTION)
    else
      AC_MSG_RESULT(cannot handle C++ exception)
    fi

fi   ####  if test ${HAVE_CXX} = yes ###


#--------------------------------------------------
# Checks for types
#--------------------------------------------------

AC_C_CONST
# 2002/06/20  AC_C_INLINE deleted by t.ohyama
# AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_CHECK_SIZEOF(short, 0)
AC_CHECK_SIZEOF(int, 0)
AC_CHECK_SIZEOF(long, 0)
AC_CHECK_SIZEOF(long long, 0)
AC_CHECK_SIZEOF(float, 0)
AC_CHECK_SIZEOF(double, 0)
AC_CHECK_SIZEOF(long double, 0)
AC_CHECK_SIZEOF(size_t, 0)
# AC_CHECK_TYPE(ssize_t, int)
AC_C_BIGENDIAN

if test "${HAVE_CXX}" = "yes" ; then
    AC_LANG_SAVE
    AC_LANG_CPLUSPLUS
    AC_CHECK_SIZEOF(bool, 0)
    AC_LANG_RESTORE
fi

AC_MSG_CHECKING(for socklen_t)
AC_CACHE_VAL(ac_cv_type_socklen_t,
[
  AC_EGREP_CPP(
    socklen_t,
    [#include <sys/socket.h>],
    ac_cv_type_socklen_t=yes, ac_cv_type_socklen_t=no
  )
])
AC_MSG_RESULT($ac_cv_type_socklen_t)
if test $ac_cv_type_socklen_t = no; then
  AC_DEFINE(socklen_t, int)
fi

#### Added 2002/06/20 by t.ohyama 
#### Checking sys/types.h and defined types int8 etc. in it.

AC_CHECK_HEADER(sys/types.h, [
    adv_have_systypes_h=yes
    AC_DEFINE(HAVE_SYSTYPES_H)
])

if test "${adv_have_systypes_h}" = "yes"; then 
    ##### Check defined types: int8  #################
    AC_MSG_CHECKING(defined type int8)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  int8;
        ], [ ] , 
             adv_have_int8=no,
             adv_have_int8=yes
    )
    AC_MSG_RESULT([$adv_have_int8])
    if test "${adv_have_int8}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_INT8)
    fi
    
    ##### Check defined types: uint8  #################
    AC_MSG_CHECKING(defined type uint8)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  uint8;
        ], [ ] , 
             adv_have_uint8=no,
             adv_have_uint8=yes
    )
    AC_MSG_RESULT([$adv_have_uint8])
    if test "${adv_have_uint8}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_UINT8)
    fi
    
    ##### Check defined types: int16  #################
    AC_MSG_CHECKING(defined type int16)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  int16;
        ], [ ] , 
             adv_have_int16=no,
             adv_have_int16=yes
    )
    AC_MSG_RESULT([$adv_have_int16])
    if test "${adv_have_int16}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_INT16)
    fi
    
    ##### Check defined types: uint16  #################
    AC_MSG_CHECKING(defined type uint16)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  uint16;
        ], [ ] , 
             adv_have_uint16=no,
             adv_have_uint16=yes
    )
    AC_MSG_RESULT([$adv_have_uint16])
    if test "${adv_have_uint16}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_UINT16)
    fi
    
    ##### Check defined types: int32  #################
    AC_MSG_CHECKING(defined type int32)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  int32;
        ], [ ] , 
             adv_have_int32=no,
             adv_have_int32=yes
    )
    AC_MSG_RESULT([$adv_have_int32])
    if test "${adv_have_int32}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_INT32)
    fi
    
    ##### Check defined types: uint32  #################
    AC_MSG_CHECKING(defined type uint32)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  uint32;
        ], [ ] , 
             adv_have_uint32=no,
             adv_have_uint32=yes
    )
    AC_MSG_RESULT([$adv_have_uint32])
    if test "${adv_have_uint32}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_UINT32)
    fi
    
    ##### Check defined types: int64  #################
    AC_MSG_CHECKING(defined type int64)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  int64;
        ], [ ] , 
             adv_have_int64=no,
             adv_have_int64=yes
    )
    AC_MSG_RESULT([$adv_have_int64])
    if test "${adv_have_int64}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_INT64)
    fi
    
    ##### Check defined types: uint64  #################
    AC_MSG_CHECKING(defined type uint64)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  uint64;
        ], [ ] , 
             adv_have_uint64=no,
             adv_have_uint64=yes
    )
    AC_MSG_RESULT([$adv_have_uint64])
    if test "${adv_have_uint64}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_UINT64)
    fi
    
    ##### Check defined types: float32  #################
    AC_MSG_CHECKING(defined type float32)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  float32;
        ], [ ] , 
             adv_have_float32=no,
             adv_have_float32=yes
    )
    AC_MSG_RESULT([$adv_have_float32])
    if test "${adv_have_float32}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_FLOAT32)
    fi
    
    ##### Check defined types: float64  #################
    AC_MSG_CHECKING(defined type float64)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  float64;
        ], [ ] , 
             adv_have_float64=no,
             adv_have_float64=yes
    )
    AC_MSG_RESULT([$adv_have_float64])
    if test "${adv_have_float64}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_FLOAT64)
    fi
    
    ##### Check defined types: float128  #################
    AC_MSG_CHECKING(defined type float128)
    AC_TRY_COMPILE(
        [#include <sys/types.h>
         typedef  char  float128;
        ], [ ] , 
             adv_have_float128=no,
             adv_have_float128=yes
    )
    AC_MSG_RESULT([$adv_have_float128])
    if test "${adv_have_float128}" = "yes"; then
        AC_DEFINE(HAVE_SYSTYPE_FLOAT128)
    fi
fi  # if  test "${adv_have_systypes_h}"
    

#--------------------------------------------------
# Checks for va_copy
#--------------------------------------------------
AC_CACHE_CHECK([for va_copy], ac_cv_c_va_copy,
  AC_TRY_LINK(
    [#include <stdarg.h>],
    [va_list ap1, ap2; va_copy(ap1,ap2);],
    [ac_cv_c_va_copy="yes"],
    [ac_cv_c_va_copy="no"]))
if test "${ac_cv_c_va_copy}" = "yes"; then
  AC_DEFINE(HAVE_VA_COPY)
fi
AC_CACHE_CHECK([for __va_copy], ac_cv_c___va_copy,
  AC_TRY_LINK(
    [#include <stdarg.h>],
    [va_list ap1, ap2; __va_copy(ap1,ap2);],
    [ac_cv_c___va_copy="yes"],
    [ac_cv_c___va_copy="no"]))
if test "${ac_cv_c___va_copy}" = "yes"; then
  AC_DEFINE(HAVE___VA_COPY)
fi

if test "${ac_cv_c_va_copy}" != "yes" -a "${ac_cv_c___va_copy}" != "yes"; then
echo  "***************** Warning ****************************************"
echo  "cannnot find \"va_copy(va2, va1)\":  using va1 = va2 instead !!!!"
echo  "******************************************************************"
fi



#--------------------------------------------------
# Checks for functions
#--------------------------------------------------

AC_CHECK_FUNC(realpath,
  AC_DEFINE(HAVE_FUNC_REALPATH)
)

AC_CHECK_FUNC(snprintf,
  AC_DEFINE(HAVE_FUNC_SNPRINTF)
)

#============================================================
# GTK
#============================================================

AM_PATH_GTK(
  1.1.14,
  adv_cv_lib_gtk=yes,
  adv_cv_lib_gtk=no
)

if test "${adv_cv_lib_gtk}" != "yes"; then
  HAVE_GTK=no
  AC_MSG_RESULT([cannot find gtk (>=1.1.14)])
else
  AC_DEFINE(HAVE_GTK)
  HAVE_GTK=yes
fi

AC_SUBST(HAVE_GTK)

#============================================================
# MICO
#============================================================

MICO_VERSION_REQ="2.2.6"

if test "${with_micover}" = ""; then
  MICO_VERSION="2.3.0"
else
  MICO_VERSION=${with_micover}
fi

AC_ARG_WITH(mico,
[  --with-mico             MICO's prefix-dir.
                          GTK+ support lib (libmicogtk) is required],,with_mico=no) 
AC_ARG_WITH(micover,
[  --with-micover          MICO's version
                          2.2.6 or higher is required, default is 2.3.0 ] )

if test "${HAVE_CXX}" = "yes" -a "${with_mico}" != "no"; then
  CXX_save=${CXX}
  CXXFLAGS_save=${CXXFLAGS}
  LIBS_save=${LIBS}
  if test "${with_mico}" = ""; then
    CXX="mico-c++"
    LIBS="-lmico${MICO_VERSION} -lmicogtk${MICO_VERSION} ${GTK_LIBS} ${LIBS}"
  else
    CXX="${with_mico}/bin/mico-c++"
    CXXFLAGS="${CXXFLAGS} -I${with_mico}/include"
    LIBS="-L${with_mico}/lib -lmico${MICO_VERSION} -lmicogtk${MICO_VERSION} ${GTK_LIBS} ${LIBS}"
  fi
  
  AC_LANG_SAVE
  AC_LANG_CPLUSPLUS
  AC_CACHE_CHECK(
    [for MICO (with GTK+ support) version ${MICO_VERSION}],
    adv_cv_lib_micogtk,
    [
      AC_TRY_LINK(
        [#include <CORBA.h>],
        [int ac; char** av; CORBA::ORB_init( ac, av, "" )],
        adv_cv_lib_micogtk=yes,
        adv_cv_lib_micogtk=no
      )
    ]
  )
  AC_LANG_RESTORE

  if test "${adv_cv_lib_micogtk}" = "no"; then
    AC_MSG_RESULT([cannot find MICO ]${MICO_VERSION}[ with GTK+ support])
  fi
  
  CXX=${CXX_save}
  CXXFLAGS=${CXXFLAGS_save}
  LIBS=${LIBS_save}
fi

if test "${adv_cv_lib_micogtk}" = "yes"; then
  HAVE_ORB=yes
  HAVE_MICO=yes
  AC_DEFINE(HAVE_ORB)
  AC_DEFINE(HAVE_MICO, ${MICO_VERSION})
  if test "${MICO_VERSION}" = "2.3.0"; then
    AC_DEFINE(HAVE_MICO_2_3_X)
  fi
  if test "${MICO_VERSION}" = "2.3.1"; then
    AC_DEFINE(HAVE_MICO_2_3_X)
  fi
  if test "${with_mico}" = ""; then
    MICO_LIBS="-lmico${MICO_VERSION} -lmicocoss${MICO_VERSION}"
  else
    MICO_LIBS="-L${with_mico}/lib -lmico${MICO_VERSION} -lmicocoss${MICO_VERSION}"
    INCLUDES="${INCLUDES} -I${with_mico}/include"
  fi
  AC_SUBST(MICO_LIBS)
  MICO_GTK_LIBS="-lmicogtk${MICO_VERSION}"
  AC_SUBST(MICO_GTK_LIBS)
else
  HAVE_MICO=no
fi

AC_SUBST(HAVE_MICO)
AM_CONDITIONAL(HAVE_MICO, test "${HAVE_MICO}" = "yes")

#============================================================
# ORBit
#============================================================

HAVE_ORBIT=no
AC_ARG_WITH(orbit,
[  --with-orbit           ORBit's prefix-dir.],,with_orbit=no)

adv_cv_lib_orbit=no
if test "${with_orbit}" != "no"; then
  AC_CHECK_PROG(ORBIT_CONFIG, orbit-config, orbit-config, :)
  if test "${ORBIT_CONFIG}" != ":"; then
    AC_MSG_CHECKING(for ORBit's version)
    ORBIT_Version=`${ORBIT_CONFIG} --version`
    ORBIT_Major=`echo ${ORBIT_Version} | sed -e 's/.*\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\).*/\1/'`
    ORBIT_Minor=`echo ${ORBIT_Version} | sed -e 's/.*\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\).*/\2/'`
    ORBIT_Micro=`echo ${ORBIT_Version} | sed -e 's/.*\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\)\.\([[0-9]][[0-9]]*\).*/\3/'`
    ORBIT_Version=${ORBIT_Major}.${ORBIT_Minor}.${ORBIT_Micro}
    if test "${ORBIT_Major}" -eq 0; then
      if test "${ORBIT_Minor}" -ge 5; then
        adv_cv_lib_orbit=yes
      fi
    fi
    if test "${adv_cv_lib_orbit}" = "yes"; then
      AC_MSG_RESULT(${ORBIT_Version}...OK)
    else
      AC_MSG_RESULT(${ORBIT_Version}...NG(old))
    fi
  fi
  
  if test ${adv_cv_lib_orbit} = "yes"; then
    AC_DEFINE(HAVE_ORB)
    AC_DEFINE(HAVE_ORBIT)
    HAVE_ORB=yes
    HAVE_ORBIT=yes
    ORBIT_CLIENT_LIBS="`orbit-config --libs client`"
    ORBIT_CLIENT_CFLAGS="`orbit-config --cflags client`"
  fi
fi

AC_SUBST(HAVE_ORBIT)
AC_SUBST(ORBIT_CLIENT_LIBS)
AC_SUBST(ORBIT_CLIENT_CFLAGS)
AM_CONDITIONAL(HAVE_ORBIT, test "${HAVE_ORBIT}" = "yes")


#============================================================
# zlib
#============================================================

AC_CHECK_LIB(z, gzrewind, [
  AC_CHECK_HEADER(zlib.h, [
    AC_DEFINE(USE_ZLIB)
    LIBZ_LIBS=-lz
  ])
])
AC_SUBST(LIBZ_LIBS)

#============================================================
# multi thread library
#============================================================

AC_MSG_CHECKING(for POSIX threads library)
case ${host} in
*linux*)
  AC_MSG_RESULT(yes)
  THREAD_LIBS=-lpthread
  ;;
*freebsd*)
  AC_MSG_RESULT(yes)
  THREAD_LIBS=-lc_r
  ;;
*solaris*)
  ;;
*)
  AC_MSG_RESULT(no)
  ;;
esac
AC_SUBST(THREAD_LIBS)

#============================================================
# MPI
#============================================================

#AC_CHECK_PROG(MPICC, mpicc, mpicc, :)
#if test "${MPICC}" != ":"; then
#  adv_cv_lib_mpi=yes
#else
#  adv_cv_lib_mpi=no
#fi

#if test ${adv_cv_lib_mpi} = "yes"; then
#  AC_DEFINE(HAVE_MPI)
#  echo mpi > conftest.c
#  echo mpi > conftest.o
#  MPI_CFLAGS=`mpicc -show -c conftest.c | sed -e 's/-c *conftest\.c//' -e 's/^[[a-pzA-Z]]* *//' -e 's/-I\.\.[[^ ]]*//g' -e 's/-DHAVE_CONFIG_H//'`
#  MPI_LIBS=`mpicc -show conftest.o | sed -e 's/conftest\.o//' -e 's/^[[a-zA-Z]]* *//' -e 's/-I[[^ ]]*//g'`
#  AC_SUBST(MPI_LIBS)
#  AC_SUBST(MPI_CFLAGS)
#  rm conftest.c conftest.o
#fi

#============================================================
# curses
#============================================================

HAVE_CURSES=no

AC_CHECK_LIB(ncurses, setupterm, [
  AC_CHECK_HEADER(term.h, [
    AC_DEFINE(HAVE_CURSES, 1)         # 1: ncurses
    HAVE_CURSES=yes
    CURSES_LIBS=-lncurses
  ])
])

if test "${HAVE_CURSES}" = "no"; then
  AC_CHECK_LIB(curses, setupterm, [
    AC_CHECK_HEADER(term.h, [
      AC_DEFINE(HAVE_CURSES, 2)       # 2: curses
      HAVE_CURSES=yes
      CURSES_LIBS=-lcurses
    ])
  ])
fi

AC_SUBST(HAVE_CURSES)
AC_SUBST(CURSES_LIBS)

#============================================================
# Python
#============================================================

AC_ARG_WITH(pyinc,
[  --with-pyinc           Python's include dir.])
Python_INCLUDES="${with_pyinc} /usr/include/python /usr/local/include/python1.5"
PYTHON_CFLAGS=""
for dir in ${Python_INCLUDES}; do
  if test -f ${dir}/Python.h; then
    PYTHON_CFLAGS="-I${dir}"
  fi
done

AC_SUBST(PYTHON_CFLAGS)
AM_CONDITIONAL(HAVE_PYTHON, test "${PYTHON_CFLAGS}" != "")

#============================================================
# Debug
#============================================================

AC_ARG_ENABLE(debug,
[  --enable-debug          enable debugging [default=no] ],
,
enable_debug=no)


if test "${GCC}" = "yes"; then
    if test "${enable_debug}" = "yes"; then
      AC_DEFINE(DEBUG)
      AC_DEFINE(DEBUG_MEMSTAT, 2)
      # CXXFLAGS=`echo ${CXXFLAGS} | sed 's/-O[[0-9]]*//' `
      # CFLAGS=`echo ${CFLAGS} | sed 's/-O[[0-9]]*//' `
      # CXXFLAGS="-O0 ${CXXFLAGS}"
      # CFLAGS="-O0 ${CFLAGS}"
    # else
      # CXXFLAGS=`echo ${CXXFLAGS} | sed 's/-O[[0-9]]*//' `
      # CFLAGS=`echo ${CFLAGS} | sed 's/-O[[0-9]]*//' `
      # CXXFLAGS="-O3 -unroll-loops ${CXXFLAGS}"
      # CFLAGS="-O3 -unroll-loops ${CFLAGS}"
    fi
else 
    if test "${enable_debug}" = "yes"; then
      AC_DEFINE(DEBUG)
      AC_DEFINE(DEBUG_MEMSTAT, 2)
      # CXXFLAGS=`echo ${CXXFLAGS} | sed 's/-O[[0-9]]*//' `
      # CFLAGS=`echo ${CFLAGS} | sed 's/-O[[0-9]]*//' `
      # CXXFLAGS="-O0 ${CXXFLAGS}"
      # CFLAGS="-O0 ${CFLAGS}"
    # else
      # CXXFLAGS=`echo ${CXXFLAGS} | sed 's/-O[[0-9]]*//' `
      # CFLAGS=`echo ${CFLAGS} | sed 's/-O[[0-9]]*//' `
      # CXXFLAGS="-O3 -unroll-loops ${CXXFLAGS}"
      # CFLAGS="-O3 -unroll-loops ${CFLAGS}"
    fi
fi

#============================================================
# for SR
#============================================================

case "${host}" in
*hiux*)
  CXXFLAGS=`echo ${CXXFLAGS} | sed 's/-O[[0-9]]*/-O/' `
  CFLAGS=`echo ${CFLAGS} | sed 's/-O[[0-9]]*/-O/' `
  CXXFLAGS=`echo ${CXXFLAGS} | sed 's/-g//' `
  CFLAGS=`echo ${CFLAGS} | sed 's/-g//' `
  AC_DEFINE(MACHINE_SR)
  ;; 
esac

#============================================================
# make includedir libdir
#============================================================

mkdir include include/Adv lib 2> /dev/null
LOCAL_INCLUDEDIR='${top_builddir}/include'
LOCAL_LIBDIR='${top_builddir}/lib'
AC_SUBST(LOCAL_INCLUDEDIR)
AC_SUBST(LOCAL_LIBDIR)

#============================================================
# Make variables
#============================================================

idldir='${top_builddir}/src/idl'
INCLUDES="-I${idldir} -I${LOCAL_INCLUDEDIR} ${INCLUDES}"
CXXFLAGS="${CXXFLAGS} ${GTK_CFLAGS}"
CFLAGS="${CFLAGS} ${GTK_CFLAGS}"
LIBS="-L${LOCAL_LIBDIR} ${LIBS}"
  
AC_SUBST(idldir)
AC_SUBST(INCLUDES)
AC_SUBST(LIBS)

ADV_BASE_ILIBS="-lAdvBase"
ADV_BASE_LIBS='${top_builddir}/lib/libAdvBase.a'
# autoconf 2.14 still dont know alphaev6
# if not should use  `case "${host}" in'
case `./config.guess` in
*-dec-*)
  ADV_BASE_ILIBS="${ADV_BASE_ILIBS} -lmach"
  ADV_BASE_LIBS="${ADV_BASE_LIBS} -lmach"
  ;;
esac

ADV_FILEIO_ILIBS="-lAdvFileIO"
ADV_FILEIO_LIBS='${top_builddir}/lib/libAdvFileIO.a'

if test "${HAVE_ORB}" = "yes"; then
  ADV_DOCIO_ILIBS='-lAdvDocIO -lAdvIDL'
  ADV_DOCIO_LIBS='${top_builddir}/lib/libAdvDocIO.a ${top_builddir}/lib/libAdvIDL.a'
else
  ADV_DOCIO_ILIBS='-lAdvDocIO'
  ADV_DOCIO_LIBS='${top_builddir}/lib/libAdvDocIO.a'
fi

if test "${HAVE_ORBIT}" = "yes"; then
  ADV_DOCIO_ILIBS="${ADV_DOCIO_ILIBS} ${ORBIT_CLIENT_LIBS}"
  ADV_DOCIO_LIBS="${ADV_DOCIO_LIBS} ${ORBIT_CLIENT_LIBS}"
  ADV_DOCIO_CFLAGS="${ORBIT_CLIENT_CFLAGS}"
fi

ADV_FRAME_ILIBS="-lAdvFrame"
ADV_FRAME_LIBS='${top_builddir}/lib/libAdvFrame.a'

AC_SUBST(ADV_BASE_ILIBS)
AC_SUBST(ADV_BASE_LIBS)
AC_SUBST(ADV_FILEIO_ILIBS)
AC_SUBST(ADV_FILEIO_LIBS)
AC_SUBST(ADV_DOCIO_ILIBS)
AC_SUBST(ADV_DOCIO_LIBS)
AC_SUBST(ADV_DOCIO_CFLAGS)
AC_SUBST(ADV_FRAME_ILIBS)
AC_SUBST(ADV_FRAME_LIBS)

#============================================================
# packages
#============================================================

AC_SUBST(subdirs)
subdirs="${subdirs} Base FileIO IDL DocIO doc"
#if test "${adv_cv_lib_micogtk}" = "yes"; then
#  subdirs="${subdirs} Frame"
#  subdirs="${subdirs} Compo"
#fi
subdirs="${subdirs} Utils"

#============================================================
# finish
#============================================================

AC_OUTPUT(
advsys-config
Makefile
Base/Makefile
FileIO/Makefile
IDL/Makefile
DocIO/Makefile
doc/Makefile
dnl Frame/Makefile
dnl Compo/Makefile
dnl Compo/sessionmgr/Makefile
Utils/Makefile
)

cp -pf advsys-config.h include/Adv
chmod +x advsys-config

echo 
echo ========================================================
echo  Configuration Report
echo ========================================================
echo
echo Host: ${host}
echo PREFIX: ${prefix}
echo

if test "${ac_cv_c_bigendian}" = "yes"; then
  echo "Endian: Big Endian"
else
  echo "Endian: Little Endian"
fi

if test "${ac_cv_sizeof_long_long}" = "8"; then
  echo  "64bit-int: yes"
else
  echo  "64bit-int: no"
fi

echo Gtk+: ${HAVE_GTK}
echo MICO: ${HAVE_MICO}
echo ORBit: ${HAVE_ORBIT}
echo DEBUG: ${enable_debug}
echo CC=${CC}
echo INCLUDES=${INCLUDES}
echo CFLAGS=${CFLAGS}
if test "${HAVE_CXX}" = "yes" ; then
  echo CXX=${CXX}
  echo CXXFLAGS=${CXXFLAGS}
else
    echo "CXX: do not use c++"
fi
echo LIBS=${LIBS}
echo PYTHON_CFLAGS=${PYTHON_CFLAGS}
echo ADV_BASE_LIBS=${ADV_BASE_LIBS}
echo ADV_FILEIO_LIBS=${ADV_FILEIO_LIBS}
echo ADV_DOCIO_LIBS=${ADV_DOCIO_LIBS}
echo ADV_FRAME_LIBS=${ADV_FRAME_LIBS}
echo

# echo make sure to use GNU-make to make.
